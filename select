select(){
	p:=var("pos"),ins:=[],sel:=[],xy:=[]
	MouseGetPos,xx,yy
	if (xx<p.Border || yy<p.Border+p.Caption){
		Gui,99:Destroy
		return
	}
	Random,color,0xaaaaaa,0xeeeeee
	gui,59:-caption +alwaysontop +E0x20 +hwndselect +Owner1
	WinGetPos,wx,wy,ww,wh,% hwnd([1])
	gui,59:show,% "x" wx " y" wy " w" ww " h" wh  " noactivate hide"
	gui,59:color,%color%
	gui,59:show,NoActivate
	WinSet,Transparent,20,ahk_id %select%
	while,GetKeyState("LButton","P"){
		mousegetpos,x,y
		winset,region,%xx%-%yy% %x%-%yy% %x%-%y% %xx%-%y% %xx%-%yy%,ahk_id %select%
	}
	Gui,59:Destroy
	adjust(x,y),adjust(xx,yy)
	xy["x",x]:=1,xy["x",xx]:=1,xy["y",y]:=1,xy["y",yy]:=1
	if (x=""||y=""||xx=""||yy="")
	return selected_controls({}),showhighlight()
	ins:=inside({x:xy.x.minindex(),w:abs(x-xx),y:xy.y.minindex(),h:abs(y-yy)})
	if GetKeyState("Control","P"){
		new:=[]
		sel:=selected_controls()
		for a,b in ins
		if !sel[a]
		new[a]:=a
		ins:=new
	}
	if GetKeyState("Shift","P")&&GetKeyState("Control","P")=0{
		for a,b in selected_controls()
		ins[a]:=a
	}
	selected_controls(ins),showhighlight()
}